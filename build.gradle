group = projectGroup
version = projectVersion

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.1.9.RELEASE'
    }
}

apply plugin: 'war'
apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'jacoco'
apply plugin: 'codenarc'

compileJava {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

mainClassName = 'com.stehno.photopile.config.PhotopileApplication'

compileGroovy {
    groovyOptions.optimizationOptions.indy = false
}

repositories {
    jcenter()
}

configurations {
    providedRuntime
}

dependencies {
    compile groovySpec

    // spring-managed dependencies

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    compile 'org.springframework.boot:spring-boot-starter-logging'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile('org.springframework.boot:spring-boot-starter-remote-shell') {
        exclude group: 'org.codehaus.groovy'
    }
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile 'org.flywaydb:flyway-core'
    compile 'com.codahale.metrics:metrics-core'

    // versioned dependencies

    compile 'org.postgresql:postgresql:9.3-1102-jdbc41'

    compile 'org.apache.tika:tika-parsers:1.6'
    compile 'org.codehaus.gpars:gpars:1.2.1'

    // verify these are needed (start)
    compile 'com.google.guava:guava:14.0.1'

    compile "org.imgscalr:imgscalr-lib:4.2"
    compile "org.apache.sanselan:sanselan:0.97-incubator"

    compile "commons-codec:commons-codec:1.8"
    compile "commons-io:commons-io:2.4"
    compile "commons-lang:commons-lang:2.6"
    // verify these are needed (end)

    // test dependencies

    testCompile 'junit:junit'
    testCompile 'com.jayway.jsonpath:json-path'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}

war {
    baseName = 'photopile'
}

// work-around to fix IDE-run test failures (may be fixed in future Gradle versions)
task copyMainResourcesToTest(type: Copy) {
    from "${projectDir}/src/main/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyMainResourcesToTest

task copyTestResourcesToTest(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResourcesToTest

test {
    jvmArgs '-Xverify:none'
}

run {
    jvmArgs '-Xverify:none'
}

codenarc {
    toolVersion = '0.22'
}

codenarcMain {
    ignoreFailures true
    configFile file('config/codenarc/codenarc-main.rules')

    maxPriority1Violations 0
    maxPriority2Violations 10
    maxPriority3Violations 10
}

codenarcTest {
    ignoreFailures true
    configFile file('config/codenarc/codenarc-test.rules')

    maxPriority1Violations 0
    maxPriority2Violations 10
    maxPriority3Violations 20
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}